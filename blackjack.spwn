random = import rand

cards = ["A", "2", "3", "4", "5", "6", "7", "8", "9", "10", "J", "Q","K"]
suits = ["D", "C", "S", "H"]
let values = {_A: 1, _2: 2, _3: 3, _4: 4, _5: 5, _6: 6, _7: 7, _8: 8, _9: 9, _10: 10, _J: 10, _Q: 10, _K: 10}

let deck = []
for card in cards {
    for suit in suits {
        deck.push("_"+card+suit)
    }
}

let full_deck = deck

get_cards = (){
    deck = random.shuffle(deck)
    return deck.pop()
}

eval_score = (cards){
    let hand = []
    for card in cards {
        hand.push(card.substr(0, card.length-1))
    }
    let vals = []
    for card in hand {
        vals.push(values[card])
    }
    let score = vals.sum()
    if (hand has "_A") && (score <= 11) {
        score += 10
    }
    return score
}

while true {
    deck = full_deck
    let player_hand = [get_cards(), get_cards()]
    let dealer_hand = [get_cards(), get_cards()]
    let player_score = eval_score(player_hand)
    let dealer_score = eval_score(dealer_hand)

    $.print(player_hand)
    let choice = $.get_input("Twist(0) or stick(1)? ")

    while choice == "0\r" {
        player_hand.push(get_cards())
        $.print(player_hand)
        player_score = eval_score(player_hand)
        if player_score > 21 {
            break
        }
        choice = $.get_input("Twist(0) or stick(1)? ")
    }

    while dealer_score < 16 {
        dealer_hand.push(get_cards())
        dealer_score = eval_score(dealer_hand)

    }

    $.print(player_hand, player_score)
    $.print(dealer_hand, dealer_score)

    if player_score > 21 && dealer_score > 21{
        $.print("Both bust, draw")
    } else if player_score > 21 {
        $.print("Bust, you lose")
    } else if dealer_score > 21 {
        $.print("Dealer bust, you win")
    } else if player_score > dealer_score {
        $.print("Higher, you win")
    }
    else if dealer_score > player_score {
        $.print("Lower, you lose")
    }
    else if dealer_score == player_score {
        if dealer_hand.length < player_hand.length {
            $.print("More cards, you win")
        }
        else if player_hand.length < dealer_hand.length {
            $.print("Less cards, you lose")
        }
        else if player_hand.length == dealer_hand.length {
            $.print("Draw")
        }
    }
}